;;*****************************************************************************
;;
;; File:        ThrowOnCommunicationError.dbl
;;
;; Description: Throw a DatabaseCommunicationException when one of a set of
;;              recognized exceptions is en countered. This will in turn cause
;;              replicator to initiate re-connect processing.
;;
;; Author:      Steve Ives (Synergex Professional Services Group)
;;
;;*****************************************************************************
;;
;; Copyright (c) 2020, Synergex International, Inc.
;; All rights reserved.
;;
;; Redistribution and use in source and binary forms, with or without
;; modification, are permitted provided that the following conditions are met:
;;
;; * Redistributions of source code must retain the above copyright notice,
;;   this list of conditions and the following disclaimer.
;;
;; * Redistributions in binary form must reproduce the above copyright notice,
;;   this list of conditions and the following disclaimer in the documentation
;;   and/or other materials provided with the distribution.
;;
;; THIS SOFTWARE IS PROVIDED BY THE COPYRIGHT HOLDERS AND CONTRIBUTORS "AS IS"
;; AND ANY EXPRESS OR IMPLIED WARRANTIES, INCLUDING, BUT NOT LIMITED TO, THE
;; IMPLIED WARRANTIES OF MERCHANTABILITY AND FITNESS FOR A PARTICULAR PURPOSE
;; ARE DISCLAIMED. IN NO EVENT SHALL THE COPYRIGHT HOLDER OR CONTRIBUTORS BE
;; LIABLE FOR ANY DIRECT, INDIRECT, INCIDENTAL, SPECIAL, EXEMPLARY, OR
;; CONSEQUENTIAL DAMAGES (INCLUDING, BUT NOT LIMITED TO, PROCUREMENT OF
;; SUBSTITUTE GOODS OR SERVICES; LOSS OF USE, DATA, OR PROFITS; OR BUSINESS
;; INTERRUPTION) HOWEVER CAUSED AND ON ANY THEORY OF LIABILITY, WHETHER IN
;; CONTRACT, STRICT LIABILITY, OR TORT (INCLUDING NEGLIGENCE OR OTHERWISE)
;; ARISING IN ANY WAY OUT OF THE USE OF THIS SOFTWARE, EVEN IF ADVISED OF THE
;; POSSIBILITY OF SUCH DAMAGE.
;;
;;*****************************************************************************

import ReplicationLibrary
import System.Collections

;;; <summary>
;;; This routine should be called after a SQL Connection error has been encountered.
;;; It looks for specific errors that indicate the SQL Server is no longer accessible
;;; for some reason, and in those cases throws a DatabaseCommunicationException.
;;; The replicator process has specific code that traps this exception and causes it
;;; to attempt to disconnect and reconnect to the database. If the database error is
;;; anything other than the specific errors processed by this routine then the error
;;; will continue to "bubble up" back into the replicator code, and will cause an error
;;; message to be logged. Replicator will then either ignore the error and continue,
;;; or terminate processing, based on its configuration settings.
;;; </summary>
;;; <param name="dberror">Database error number from %SSC_GETEMSG</param>
;;; <param name="errtxt">Database error text from %SSC_GETEMSG</param>
subroutine ThrowOnCommunicationError
    required in routine, string
    required in dberror, int
    required in errtxt, a
    static record
        messages, [#]string
    endrecord
proc
    if (messages == ^null)
    begin
        messages = new string[#] {
        &    "Network connection lost (recv): errno: 10060",
        &    "Shared Memory Provider: No process is on the other end of the pipe",
        &    "Network connection lost (send): An existing connection was forcibly closed by the remote host.",
        &    "CONFIG: Expected a CONFIG call"
        &    }
    end

    ;Turn off network adapter on server
    ;   dberror    -1
    ;   errtxt     Network connection lost (recv): errno: 10060

    ;Take SQL Server database off-line
    ;Stop SQL Server process
    ;   dberror    -233
    ;   errtxt     [Microsoft][ODBC Driver 17 for SQL Server]Shared Memory Provider: No process is on the other end of the pipe.\r\n\0[Microsoft][ODBC Driver 17 for SQL Server]Communication link failure

    ;Stop the OpenNet server once a connection is established
    ;   dberror    ???
    ;   errtxt     Network connection lost (send): An existing connection was forcibly closed by the remote host.

    ;Interfere with network packets going to FileService. Produced with Clumsy using the "Tamper" function and the rule "inbound and tcp.DstPort == 8080"
    ;Not currently trapping this one because it could be caused by bad data in the delimited file that we send.
    ;   dberror    -4863
    ;   errtxt     [Microsoft][ODBC Driver 17 for SQL Server][SQL Server]Bulk load data conversion error (truncation) for row 3, column 132 (Cusccall).

    ;Caused by bad data, e.g. non-numeric characters in a numeric field
    ;   dberror    -4864
    ;   errtxt     [Microsoft][ODBC Driver 17 for SQL Server][SQL Server]Bulk load data conversion error (type mismatch or invalid character for the specified codepage) for row 51793, column 4 (Famt1).\0

    ;Shut down SQL Server host
    ;   dberror    ???
    ;   errtxt     ???

    if (errtxt)
    begin
        foreach data message in messages
        begin
            if (%instr(1,errtxt,(a)message))
            begin
                throw new DatabaseCommunicationException(routine,dberror,%atrimtostring(errtxt))
            end
        end
    end

    xreturn

endsubroutine
