;;*****************************************************************************
;;
;; Routine:     ValidateBasicEnvironment
;;
;; Description: Check we have exe, data and log directories, an instance name,
;;              and a configuration file.
;;
;; Author:      Steve Ives, Synergex Professional Services Group
;;
;; Created:     26th February 2024
;;
;;*****************************************************************************
;;

import ReplicationLibrary
import System.Collections
import System.Text

function ValidateBasicEnvironment, boolean
    required in openLogFile, boolean
    required out errorMessage, string
    stack record
        ok,             boolean
        clValues,       @ArrayList
        envval,         a1024
        envlen,         i4
        tmpch,          i4

        dataDirectory,  string
        logDirectory,   string
        instanceName,   string
    endrecord
proc
    ok = true
    errorMessage = ""

    ;Is REPLICATOR_EXE defined

    xcall getlog("REPLICATOR_EXE",envval,envlen)
    if (!envlen)
    begin
        errorMessage = "Environment variable REPLICATOR_EXE is not defined"
        ok = false
    end

    if (ok)
    begin
        ;Is -datadir is specified?

        if (CommandLineParser.Parse("datadir",clValues))
        begin
            using clValues.Count select
            (1),
            begin
                xcall setlog("REPLICATOR_DATA",(string)clValues[0],envlen)
            end
            (0),
            begin
                errorMessage = "Missing value after the -datadir option"
                ok = false
            end
            (),
            begin
                errorMessage = "Only one value may follow the -datadir option"
                ok = false
            end
            endusing
        end

        if (!ok) exit

        ; Is REPLICATOR_DATA: defined?

        xcall getlog("REPLICATOR_DATA",envval,envlen)
        if (envlen) then
        begin
            dataDirectory = envval(1,envlen)
        end
        else
        begin
            errorMessage = "Pass -datadir or set environment variable REPLICATOR_DATA"
            ok = false
        end

        if (!ok) exit

        ; Is REPLICATOR_DATA:ReplicatorConfig.json present?

        try
        begin
            open(tmpch=0,i,"REPLICATOR_DATA:ReplicatorConfig.json")
        end
        catch (ex)
        begin
            errorMessage = "REPLICATOR_DATA:ReplicatorConfig.json was not found!"
            ok = false
        end
        finally
        begin
            if (tmpch)
            begin
                close tmpch
                tmpch = 0
            end
        end
        endtry

        if (!ok) exit

        ;Is -logdir is specified?

        if (CommandLineParser.Parse("logdir",clValues))
        begin
            using clValues.Count select
            (1),
            begin
                xcall setlog("REPLICATOR_LOGDIR",(string)clValues[0],envlen)
            end
            (0),
            begin
                errorMessage = "Missing value after the -logdir option"
                ok = false
            end
            (),
            begin
                errorMessage = "Only one value may follow the -logdir option"
                ok = false
            end
            endusing
        end

        if (!ok) exit

        ; Is REPLICATOR_LOGDIR: defined?

        xcall getlog("REPLICATOR_LOGDIR",envval,envlen)
        if (envlen) then
        begin
            logDirectory = envval(1,envlen)
        end
        else
        begin
            errorMessage = "Pass -logdir or set environment variable REPLICATOR_LOGDIR"
            ok = false
        end

        if (!ok) exit

        ; Does REPLICATOR_LOGDIR point to an actual directory where we can create files?

        try
        begin
            open(tmpch=0,o:s,"REPLICATOR_LOGDIR:tmp.tmp")
        end
        catch (e, @Exception)
        begin
            errorMessage = "Can't create files in REPLICATOR_LOGDIR:"
            ok = false
        end
        finally
        begin
            if (tmpch)
            begin
                purge tmpch
                tmpch = 0
            end
        end
        endtry
        
        if (!ok) exit

        ; Check we have an instance name on the command line

        if (CommandLineParser.Parse("instance",clValues))
        begin
            using clValues.Count select
            (1),
            begin
                xcall setlog("REPLICATOR_INSTANCE",((string)clValues[0]).ToUpper(),envlen)
            end
            (0),
            begin
                Logger.ErrorLog("Missing value after the -instance option")
                ok = false
            end
            (),
            begin
                Logger.ErrorLog("Only one value may follow the -instance option")
                ok = false
            end
            endusing
        end

        if (!ok) exit

        ; If not on the command line the instance name can also come from REPLICATOR_INSTANCE

        xcall getlog("REPLICATOR_INSTANCE",envval,envlen)
        if (envlen) then
        begin
            instanceName = ((string)envval(1,envlen)).ToUpper()
        end
        else
        begin
            instanceName = "DEFAULT"
        end

        ; Initialize the settings object and set the instance name. This also sets the following:
        ;   Settings.QueueFileName
        ;   Settings.LogFileName
        ;   Settings.KafkaTopicName
        ;   Default values for all other properties

        Settings.Initialize()
        Settings.InstanceName = instanceName

        ; Get logging up and running

        if (openLogFile)
        begin
            if (!Logger.OpenLog())
            begin
                errorMessage = "Failed to initiate logging"
                ok = false
            end
        end
       
    end

    freturn ok

endfunction
