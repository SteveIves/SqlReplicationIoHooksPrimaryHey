;;*****************************************************************************
;;
;; File:        OpenOrCreateQueueFile.dbl
;;
;; Description: Open or create a replicator instance queue file.
;;
;; Author:      Steve Ives, Synergex Professional Services Group
;;
;;*****************************************************************************
;;
;; Copyright (c) 2019, Synergex International, Inc.
;; All rights reserved.
;;
;; Redistribution and use in source and binary forms, with or without
;; modification, are permitted provided that the following conditions are met:
;;
;; * Redistributions of source code must retain the above copyright notice,
;;   this list of conditions and the following disclaimer.
;;
;; * Redistributions in binary form must reproduce the above copyright notice,
;;   this list of conditions and the following disclaimer in the documentation
;;   and/or other materials provided with the distribution.
;;
;; THIS SOFTWARE IS PROVIDED BY THE COPYRIGHT HOLDERS AND CONTRIBUTORS "AS IS"
;; AND ANY EXPRESS OR IMPLIED WARRANTIES, INCLUDING, BUT NOT LIMITED TO, THE
;; IMPLIED WARRANTIES OF MERCHANTABILITY AND FITNESS FOR A PARTICULAR PURPOSE
;; ARE DISCLAIMED. IN NO EVENT SHALL THE COPYRIGHT HOLDER OR CONTRIBUTORS BE
;; LIABLE FOR ANY DIRECT, INDIRECT, INCIDENTAL, SPECIAL, EXEMPLARY, OR
;; CONSEQUENTIAL DAMAGES (INCLUDING, BUT NOT LIMITED TO, PROCUREMENT OF
;; SUBSTITUTE GOODS OR SERVICES; LOSS OF USE, DATA, OR PROFITS; OR BUSINESS
;; INTERRUPTION) HOWEVER CAUSED AND ON ANY THEORY OF LIABILITY, WHETHER IN
;; CONTRACT, STRICT LIABILITY, OR TORT (INCLUDING NEGLIGENCE OR OTHERWISE)
;; ARISING IN ANY WAY OUT OF THE USE OF THIS SOFTWARE, EVEN IF ADVISED OF THE
;; POSSIBILITY OF SUCH DAMAGE.
;;
;;*****************************************************************************

;;; <summary>
;;; Opens (creating if necessary) a replicator instance queue file.
;;; </summary>
;;; <param name="aQueueFileSpec">Queue file specification.</param>
;;; <param name="aChannel">Returned channel number.</param>
;;; <param name="aErrorMessage">Retunred error message (only of return value is false).</param>
;;; <returns>Returns true on success.</returns>
function OpenOrCreateQueueFile, boolean
    required in  aQueueFileSpec, string
    required out aChannel, int
    required out aErrorMessage, string
    stack record
        ok, boolean
    endrecord
proc
    repeat
    begin
        try
        begin
            open(aChannel=0,"U:I",aQueueFileSpec)
            ok = true
            exitloop
        end
        catch (e, @NoFileFoundException)
        begin
.ifdef D_VMS
            xcall isamc(aQueueFileSpec + ", FIXED", 32054, 1, "START=1, LENGTH=20, TYPE=ALPHA, NAME=TRANSACTION_ID, DENSITY=100")
.else
            xcall isamc(aQueueFileSpec + ", FIXED, COMPRESS, PAGE=1024, TBYTE", 65042, 1, "START=1, LENGTH=8, TYPE=SEQUENCE, NAME=TRANSACTION_ID, DENSITY=100")
.endc
            nextloop
        end
        catch (e, @Exception)
        begin
            aErrorMessage = e.Message
            ok = false
            exitloop
        end
        endtry
    end

    freturn ok

endfunction