;;*****************************************************************************
;;

import ReplicationLibrary

.ifdef DBLNET

subroutine SendEmail
    required in subject, string
    required in body, [#]string
    required in bodyhtml, boolean
    required in recipientEmail, string
proc
    ;TODO: DOTNET: Need to implement email for .NET
    xreturn
endsubroutine

.else

subroutine SendEmail
    required in subject, string
    optional in body, [#]string
    optional in bodyhtml, boolean
    optional in recipientEmail, string

    .include "REPLICATOR_INCLUDE:SmtpMail.def"

    stack record
        emailStatus, i4
        recipientAddress, string
        bodyData, [#]string
        html, boolean
    endrecord
proc
    emailStatus = 0
    if ^passed(bodyhtml) then
        html = bodyhtml
    else
        html = false

    if (^passed(body) && body != ^null) then
    begin
        bodyData = body
    end
    else
    begin
        bodyData = new string[#] { "" }
    end

    if (^passed(recipientEmail) && recipientEmail != ^null && recipientEmail.Length>0) then
    begin
        recipientAddress = recipientEmail
    end
    else
    begin
        recipientAddress = Settings.ErrorEmail
    end

    if ((emailStatus=%SmtpMail(Settings.EmailServer,Settings.EmailSender,"SQL Replicator",,recipientAddress,,subject,bodyData,,html)) != SMERR_SUCCESS)
    begin
        Logger.ErrorLog("Failed to send error email. Error was " + %atrim(SmtpErrorText[emailStatus]))
    end

    xreturn

endsubroutine

.endc
