;;*****************************************************************************
;;

subroutine SendEmail
    required in aSubject, string
    required in aBody, [#]string
    required in aBodyHtml, boolean
    stack record
        bodyToSend, [#]string
    endrecord
    external function
        DoSendEmail, boolean
    endexternal
proc
    if (EmailSettings.EmailPaused)
    begin
        ;Is it time to start sending emails again?
        data timeNow, i8, %datetime_to_i8(%datetime)
        if (timeNow > (EmailSettings.TimePaused+(EmailSettings.ErrorPauseMinutes*60000000)))
        begin
            ;Yes, the pause period has expired.
            EmailSettings.ErrorCount = 0
            EmailSettings.EmailPaused = false
            Logger.Log("Email notification resuming after a " + %string(EmailSettings.ErrorPauseMinutes) + " minute pause")
        end
    end

    if (!EmailSettings.EmailPaused)
    begin
        if (aBody == ^null) then
        begin
            bodyToSend = new string[1]
            bodyToSend[1] = ""
        end
        else
        begin
            bodyToSend = aBody
        end

        if (!%DoSendEmail(aSubject,bodyToSend,aBodyHtml))
        begin
            Logger.ErrorLog("Failed to send email!")
            EmailSettings.ErrorCount = EmailSettings.ErrorCount + 1

            ;Have we exceded the SmtpMaxFail threshold?
            if (EmailSettings.ErrorCount > EmailSettings.ErrorsBeforePause)
            begin
                ;Yes, pause email delivery
                EmailSettings.EmailPaused = true
                EmailSettings.TimePaused = %datetime_to_i8(%datetime)
                Logger.ErrorLog("Email notifications will be paused for " + %string(EmailSettings.ErrorPauseMinutes)+ " following " + %string(EmailSettings.ErrorsBeforePause) + " failures")
            end
        end
    end

    xreturn

endsubroutine

.ifdef DBLNET

import ReplicationLibrary
import System.Net
import System.Net.Mail
import System.Text

function DoSendEmail, boolean
    required in aSubject, string
    required in aBody, [#]string
    required in aBodyHtml, boolean
    stack record
        ok, boolean
    endrecord
proc
    ok = true

    ;Build the message body

    data bodyBuilder = new StringBuilder()
    data bodyLine, string
    foreach bodyLine in aBody
    begin
        bodyBuilder.AppendLine(bodyLine)
    end

    ;Create a new MailMessage

    disposable data message = new MailMessage() {
    &   From = new MailAddress(EmailSettings.SenderAddress,EmailSettings.SenderName),
    &   Subject = aSubject,
    &   Body = bodyBuilder.ToString(),
    &   IsBodyHtml = aBodyHtml
    & }

    ;Add recipient(s)
    data recipient, string
    foreach recipient in EmailSettings.Recipients
    begin
        message.To.Add(new MailAddress(recipient))
    end

    ;Create an SmtpClient

    disposable data client = new SmtpClient() {
    &   Host = EmailSettings.SmtpServer,
    &   Port = EmailSettings.SmtpPort,
    &   EnableSsl = EmailSettings.SmtpUseSSL
    & }

    ;Do we have SMTP credentials?

    if (!String.IsNullOrWhiteSpace(EmailSettings.SmtpUsername) && !String.IsNullOrWhiteSpace(EmailSettings.SmtpPassword))
    begin
        client.Credentials = new NetworkCredential(EmailSettings.SmtpUsername,EmailSettings.SmtpPassword)
    end

    ;Send the message

    try
    begin
        client.SendMailAsync(message).Wait()
    end
    catch (ex, @Exception)
    begin
        ok = false
    end
    endtry
    
    freturn ok

endfunction

.else

import ReplicationLibrary

function DoSendEmail, boolean
    required in aSubject, string
    required in aBody, [#]string
    required in aBodyHtml, boolean

    .include "REPLICATOR_INCLUDE:SmtpMail.def"

    stack record
        ok, boolean
        emailStatus, i4
    endrecord
proc
    ok = true
    emailStatus = 0

    if emailStatus=%SmtpMail(
    &   EmailSettings.SmtpServer,
    &   EmailSettings.SenderAddress,
    &   EmailSettings.SenderName,
    &   ,
    &   EmailSettings.GetRecipientsString(),
    &   ,
    &   aSubject,
    &   aBody,
    &   ,
    &   aBodyHtml
    & ) != SMERR_SUCCESS
    begin
        ok = false
    end

    freturn ok

endfunction

.endc
