;;*****************************************************************************
;;

.ifdef DBLNET

import ReplicationLibrary
import System.Net
import System.Net.Mail
import System.Text

subroutine SendEmail
    required in aSubject, string
    required in aBody, [#]string
    required in aBodyHtml, boolean
proc

    ;Build the message body

    data bodyBuilder = new StringBuilder()
    if (aBody == ^null) then
    begin
        bodyBuilder.Append(String.Empty)
    end
    else
    begin
        data bodyLine, string
        foreach bodyLine in aBody
        begin
            bodyBuilder.AppendLine(bodyLine)
        end
    end

    ;Create a new MailMessage

    disposable data message = new MailMessage() {
    &   From = new MailAddress(Settings.EmailSenderAddress,Settings.EmailSenderName),
    &   Subject = aSubject,
    &   Body = bodyBuilder.ToString(),
    &   IsBodyHtml = aBodyHtml
    & }

    ;Add recipient(s)
    data recipient, string
    foreach recipient in Settings.EmailRecipients
    begin
        message.To.Add(new MailAddress(recipient))
    end

    ;Create an SmtpClient

    disposable data client = new SmtpClient() {
    &   Host = Settings.SmtpServer,
    &   Port = Settings.SmtpPort,
    &   EnableSsl = Settings.SmtpUseSSL
    & }

    ;Do we have SMTP credentials?

    if (!String.IsNullOrWhiteSpace(Settings.SmtpUsername) && !String.IsNullOrWhiteSpace(Settings.SmtpPassword))
    begin
        client.Credentials = new NetworkCredential(Settings.SmtpUsername,Settings.SmtpPassword)
    end

    ;Send the message

    try
    begin
        client.SendMailAsync(message).Wait()
    end
    catch (ex, @Exception)
    begin
        ;Noting useful to do if it fails!
        nop
    end
    endtry
    
    xreturn

endsubroutine

.else

import ReplicationLibrary

subroutine SendEmail
    required in subject, string
    required in body, [#]string
    required in bodyhtml, boolean

    .include "REPLICATOR_INCLUDE:SmtpMail.def"

    stack record
        emailStatus, i4
    endrecord
proc
    emailStatus = 0

    if emailStatus=%SmtpMail(
    &   Settings.SmtpServer,
    &   Settings.EmailSenderAddress,
    &   Settings.EmailSenderName,
    &   ,
    &   Settings.GetEmailRecipientsString(),
    &   ,
    &   subject,
    &   body,
    &   ,
    &   bodyhtml
    & ) != SMERR_SUCCESS
    begin
        Logger.ErrorLog("Failed to send error email. Error was " + %atrim(SmtpErrorText[emailStatus]))
    end

    xreturn

endsubroutine

.endc
