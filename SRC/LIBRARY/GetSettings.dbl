;;*****************************************************************************
;;
;; Routine:     GetSettings
;;
;; Description: Get settings from enmvironment variables and command line options.
;;
;; Author:      Steve Ives, Synergex Professional Services Group
;;
;; Created:     30th December 2019
;;
;;*****************************************************************************
;;

import ReplicationLibrary
import System.Collections
import System.Text
import System.Text.Json

function GetSettings,   boolean
    required out errorMessage, string
    stack record
        ok,             boolean
        clValues,       @ArrayList
        length,         int
        envval,         a1024
        envlen,         i4
        doc,            @JsonDocument
.ifdef DBLNET
        instances,      JsonElement
        instance,       JsonElement
        setting,        JsonElement
        smtpsettings,   JsonElement
        emailAddress,   JsonElement
.else
        instances,      @JsonElement
        instance,       @JsonElement
        setting,        @JsonElement
        smtpsettings,   @JsonElement
        emailAddress,   @JsonElement
.endc
    endrecord
    external function
        xsubr,  ^val
    endexternal
proc
    ok = true
    errorMessage = ""

    ;Parse the JSON configuration file

    try
    begin
        doc = Settings.ParseConfigFile()
    end
    catch (e, @Exception)
    begin
        errorMessage = "Failed to parse configuration file!"
        ok = false
    end
    endtry

    ;Get the root element

    ;Validate the instances property

    if (ok)
    begin
        if (!doc.RootElement.TryGetProperty("Instances",instances)) then
        begin
            errorMessage = "Configuration file does not have a root Instances property!"
            ok = false
        end
        else if (instances.ValueKind != JsonValueKind.Array) then
        begin
            errorMessage = "Configuration file instances property is not an array!"
            ok = false
        end
        else if (instances.GetArrayLength() == 0)
        begin
            errorMessage = "Configuration file instances property is an empty array!"
            ok = false
        end
    end

    ;Find the instance configuration for our instance name

    if (ok)
    begin
        data instanceCount = instances.GetArrayLength()
        data instanceNumber, int

        for instanceNumber from 0 thru instanceCount - 1
        begin
            instance = instances[instanceNumber]
            ;Is it our settings?
            if (!instance.TryGetProperty("InstanceName",setting)) then
            begin
                errorMessage = "Instance " + %string(instanceNumber) + " has no InstanceName property!"
                ok = false
            end
            else if (setting.ValueKind != JsonValueKind.String) then
            begin
                errorMessage = "Instance " + %string(instanceNumber) + "'s InstanceName property is not a string!"
                ok = false
            end
            else if (setting.GetString().ToUpper() != Settings.InstanceName) 
            begin
                ;Not the one we're looking for
                nextloop
            end
        end
    end

    ;We found the settings for our instance. Now process all the settings.

    ;BulkLoadBatchSize

    if (ok)
    begin
        if (instance.TryGetProperty("BulkLoadBatchSize",setting))
        begin
            if (setting.ValueKind != JsonValueKind.Number) then
            begin
                errorMessage = "The instance's BulkLoadBatchSize property is not a number!"
                ok = false
            end
            else if (setting.GetInt32() < 1000) then
            begin
                errorMessage = "The instance's BulkLoadBatchSize property may not be less than 1000!"
                ok = false
            end
            else
            begin
                Settings.BulkLoadBatchSize = setting.GetInt32()
            end
        end
    end

    ;BulkLoadTimeout

    if (ok)
    begin
        if (instance.TryGetProperty("BulkLoadTimeout",setting))
        begin
            if (setting.ValueKind != JsonValueKind.Number) then
            begin
                errorMessage = "The instance's BulkLoadTimeout property is not a number!"
                ok = false
            end
            else if (setting.GetInt32() < 60) then
            begin
                errorMessage = "The instance's BulkLoadTimeout property may not be less than 60!"
                ok = false
            end
            else
            begin
                Settings.BulkLoadTimeout = setting.GetInt32()
            end
        end
    end

    ;CommitBatchSize

    if (ok)
    begin
        if (instance.TryGetProperty("CommitBatchSize",setting))
        begin
            if (setting.ValueKind != JsonValueKind.Number) then
            begin
                errorMessage = "The instance's CommitBatchSize property is not a number!"
                ok = false
            end
            else if (setting.GetInt32() < 100) then
            begin
                errorMessage = "The instance's CommitBatchSize property may not be less than 100!"
                ok = false
            end
            else
            begin
                Settings.CommitBatchSize = setting.GetInt32()
            end
        end
    end

    ;DatabaseCommitMode

    if (ok)
    begin
        if (instance.TryGetProperty("DatabaseCommitMode",setting))
        begin
            if (setting.ValueKind != JsonValueKind.String) then
            begin
                errorMessage = "The instance's DatabaseCommitMode property is not a string!"
                ok = false
            end
            else if (String.IsNullOrWhitespace(setting.GetString())) then
            begin
                errorMessage = "The instance's DatabaseCommitMode property is blank!"
                ok = false
            end
            else
            begin
                using setting.GetString().ToUpper() select
                ("AUTOMATIC"),
                begin
                    Settings.DatabaseCommitMode = DatabaseCommitMode.Automatic
                end
                ("BATCH"),
                begin
                    Settings.DatabaseCommitMode = DatabaseCommitMode.Batch
                end
                ("MANUAL"),
                begin
                    Settings.DatabaseCommitMode = DatabaseCommitMode.Manual
                end
                (),
                begin
                    errorMessage = "The instance's DatabaseCommitMode property must be set to Automatic, Batch or Manual!"
                    ok = false
                end
                endusing
            end
        end
    end

    ;DatabaseConnectMode

    if (ok)
    begin
        if (instance.TryGetProperty("DatabaseConnectMode",setting))
        begin
            if (setting.ValueKind != JsonValueKind.String) then
            begin
                errorMessage = "The instance's DatabaseConnectMode property is not a string!"
                ok = false
            end
            else if (String.IsNullOrWhitespace(setting.GetString())) then
            begin
                errorMessage = "The instance's DatabaseConnectMode property is blank!"
                ok = false
            end
            else
            begin
                using setting.GetString().ToUpper() select
                ("SQLCONNECTION"),
                begin
                    Settings.DatabaseConnectMode = DatabaseConnectionMode.SqlConnection
                end
                ("SQLCLIENT"),
                begin
.ifdef DBLNET
                    Settings.DatabaseConnectMode = DatabaseConnectionMode.SqlClient
.else
                    errorMessage = "Connection mode " + setting.GetString() + " is only valid when running the .NET version of replicator."
                    ok = false
.endc
                end
                (),
                begin
                    errorMessage = "The instance's DatabaseConnectMode property must be set to SqlConnection, SqlClient!"
                    ok = false
                end
                endusing
            end
        end
    end

    ;DatabaseConnectString

    if (ok)
    begin
        if (instance.TryGetProperty("DatabaseConnectString",setting))
        begin
            if (setting.ValueKind != JsonValueKind.String) then
            begin
                errorMessage = "The instance's DatabaseConnectString property is not a string!"
                ok = false
            end
            else if (String.IsNullOrWhitespace(setting.GetString())) then
            begin
                errorMessage = "The instance's DatabaseConnectString property is blank!"
                ok = false
            end
            else
            begin
                Settings.DatabaseConnectString = setting.GetString()
            end
        end
    end

    ;DatabaseRetryDelay

    if (ok)
    begin
        if (instance.TryGetProperty("DatabaseRetryDelay",setting))
        begin
            if (setting.ValueKind != JsonValueKind.Number) then
            begin
                errorMessage = "The instance's DatabaseRetryDelay property is not a number!"
                ok = false
            end
            else if (setting.GetInt32() < 1) then
            begin
                errorMessage = "The instance's DatabaseRetryDelay property may not be less than 1!"
                ok = false
            end
            else
            begin
                Settings.DatabaseRetryDelay = setting.GetInt32()
            end
        end
    end

    ;DatabaseRetryMax

    if (ok)
    begin
        if (instance.TryGetProperty("DatabaseRetryMax",setting))
        begin
            if (setting.ValueKind != JsonValueKind.Number) then
            begin
                errorMessage = "The instance's DatabaseRetryMax property is not a number!"
                ok = false
            end
            else if (setting.GetInt32() < 1) then
            begin
                errorMessage = "The instance's DatabaseRetryMax property may not be less than 1!"
                ok = false
            end
            else
            begin
                Settings.DatabaseRetryMax = setting.GetInt32()
            end
        end
    end

    ;DatabaseTimeout

    if (ok)
    begin
        if (instance.TryGetProperty("DatabaseTimeout",setting))
        begin
            if (setting.ValueKind != JsonValueKind.Number) then
            begin
                errorMessage = "The instance's DatabaseTimeout property is not a number!"
                ok = false
            end
            else if (setting.GetInt32() < 1) then
            begin
                errorMessage = "The instance's DatabaseTimeout property may not be less than 1!"
                ok = false
            end
            else
            begin
                Settings.DatabaseTimeout = setting.GetInt32()
            end
        end
    end

    ;DataCompressionMode

    if (ok)
    begin
        if (instance.TryGetProperty("DataCompressionMode",setting))
        begin
            if (setting.ValueKind != JsonValueKind.String) then
            begin
                errorMessage = "The instance's DataCompressionMode property is not a string!"
                ok = false
            end
            else if (String.IsNullOrWhitespace(setting.GetString())) then
            begin
                errorMessage = "The instance's DataCompressionMode property is blank!"
                ok = false
            end
            else
            begin
                using setting.GetString().ToUpper() select
                ("NONE"),
                begin
                    Settings.DataCompressionMode = DatabaseDataCompression.None
                end
                ("PAGE"),
                begin
                    Settings.DataCompressionMode = DatabaseDataCompression.Page
                end
                ("ROW"),
                begin
                    Settings.DataCompressionMode = DatabaseDataCompression.Row
                end
                (),
                begin
                    errorMessage = "The instance's DataCompressionMode property must be set to None, Page or Row!"
                    ok = false
                end
                endusing
            end
        end
    end

    ;Enabled

    if (ok)
    begin
        if (instance.TryGetProperty("Enabled",setting))
        begin
            if (setting.ValueKind != JsonValueKind.True && setting.ValueKind != JsonValueKind.False) then
            begin
                errorMessage = "The instance's Enabled property is not a boolean!"
                ok = false
            end
            else
            begin
                Settings.Enabled = setting.GetBoolean()
            end
        end
    end

    ;ErrorSleepTime

    if (ok)
    begin
        if (instance.TryGetProperty("ErrorSleepTime",setting))
        begin
            if (setting.ValueKind != JsonValueKind.Number) then
            begin
                errorMessage = "The instance's ErrorSleepTime property is not a number!"
                ok = false
            end
            else if (setting.GetDecimal() <= 0) then
            begin
                errorMessage = "The instance's ErrorSleepTime property may not be less than or equal to zero!"
                ok = false
            end
            else
            begin
                Settings.ErrorSleepTime = setting.GetDecimal()
            end
        end
    end

    ;FileServiceHost

    if (ok)
    begin
        if (instance.TryGetProperty("FileServiceHost",setting))
        begin
            if (setting.ValueKind != JsonValueKind.String) then
            begin
                errorMessage = "The instance's FileServiceHost property is not a string!"
                ok = false
            end
            else if (!String.IsNullOrWhitespace(setting.GetString()))
            begin
                Settings.FileServiceHost = setting.GetString()
            end
        end
    end

    ;FileServicePort

    if (ok)
    begin
        if (instance.TryGetProperty("FileServicePort",setting))
        begin
            if (setting.ValueKind != JsonValueKind.Number) then
            begin
                errorMessage = "The instance's FileServicePort property is not a number!"
                ok = false
            end
            else if (setting.GetInt32() <= 0) then
            begin
                errorMessage = "The instance's FileServicePort property may not be less than or equal to zero!"
                ok = false
            end
            else
            begin
                Settings.FileServicePort = setting.GetInt32()
            end
        end
    end

    ;LocalExportPath

    if (ok)
    begin
        if (instance.TryGetProperty("LocalExportPath",setting))
        begin
            if (setting.ValueKind != JsonValueKind.String) then
            begin
                errorMessage = "The instance's LocalExportPath property is not a string!"
                ok = false
            end
            else if (!String.IsNullOrWhitespace(setting.GetString()))
            begin
                ;Check that we can create a file in the directory
                data tmpch, i4
                try
                begin
                    data tmpfile = setting.GetString() + %datetime + ".tmp"
                    open(tmpch=0,o,tmpfile)
                    Settings.LocalExportPath = setting.GetString()
                end
                catch (e, @Exception)
                begin
                    errorMessage = "The instance's LocalExportPath property is invalid!"
                    ok = false
                end
                finally
                begin
                    if (tmpch && %chopen(tmpch))
                        close tmpch
                end
                endtry
            end
        end
    end

    ;LogBulkLoadExceptions

    if (ok)
    begin
        if (instance.TryGetProperty("LogBulkLoadExceptions",setting))
        begin
            if (setting.ValueKind != JsonValueKind.True && setting.ValueKind != JsonValueKind.False) then
            begin
                errorMessage = "The instance's LogBulkLoadExceptions property is not a boolean!"
                ok = false
            end
            else
            begin
                Settings.LogBulkLoadExceptions = setting.GetBoolean()
            end
        end
    end

    ;LogKeyValues

    if (ok)
    begin
        if (instance.TryGetProperty("LogKeyValues",setting))
        begin
            if (setting.ValueKind != JsonValueKind.True && setting.ValueKind != JsonValueKind.False) then
            begin
                errorMessage = "The instance's LogKeyValues property is not a boolean!"
                ok = false
            end
            else
            begin
                Settings.LogKeyValues = setting.GetBoolean()
            end
        end
    end

    ;LogLoadProgress

    if (ok)
    begin
        if (instance.TryGetProperty("LogLoadProgress",setting))
        begin
            if (setting.ValueKind != JsonValueKind.True && setting.ValueKind != JsonValueKind.False) then
            begin
                errorMessage = "The instance's LogLoadProgress property is not a boolean!"
                ok = false
            end
            else
            begin
                Settings.LogLoadProgress = setting.GetBoolean()
            end
        end
    end

    ;MaxColumns

    if (ok)
    begin
        if (instance.TryGetProperty("MaxColumns",setting))
        begin
            if (setting.ValueKind != JsonValueKind.Number) then
            begin
                errorMessage = "The instance's MaxColumns property is not a number!"
                ok = false
            end
            else if (setting.GetInt32() < 1) then
            begin
                errorMessage = "The instance's MaxColumns property may not be less than 1!"
                ok = false
            end
            else
            begin
                Settings.MaxColumns = setting.GetInt32()
            end
        end
    end

    ;MaxCursors

    if (ok)
    begin
        if (instance.TryGetProperty("MaxCursors",setting))
        begin
            if (setting.ValueKind != JsonValueKind.Number) then
            begin
                errorMessage = "The instance's MaxCursors property is not a number!"
                ok = false
            end
            else if (setting.GetInt32() < 1) then
            begin
                errorMessage = "The instance's MaxCursors property may not be less than 1!"
                ok = false
            end
            else
            begin
                Settings.MaxCursors = setting.GetInt32()
            end
        end
    end

    ;QueueReconnectAttempts

    if (ok)
    begin
        if (instance.TryGetProperty("QueueReconnectAttempts",setting))
        begin
            if (setting.ValueKind != JsonValueKind.Number) then
            begin
                errorMessage = "The instance's QueueReconnectAttempts property is not a number!"
                ok = false
            end
            else if (setting.GetInt32() < 1) then
            begin
                errorMessage = "The instance's QueueReconnectAttempts property may not be less than 1!"
                ok = false
            end
            else
            begin
                Settings.QueueReconnectAttempts = setting.GetInt32()
            end
        end
    end

    ;QueueReconnectDelay

    if (ok)
    begin
        if (instance.TryGetProperty("QueueReconnectDelay",setting))
        begin
            if (setting.ValueKind != JsonValueKind.Number) then
            begin
                errorMessage = "The instance's QueueReconnectDelay property is not a number!"
                ok = false
            end
            else if (setting.GetInt32() < 1) then
            begin
                errorMessage = "The instance's QueueReconnectDelay property may not be less than 1!"
                ok = false
            end
            else
            begin
                Settings.QueueReconnectDelay = setting.GetInt32()
            end
        end
    end

    ;QueueType

    if (ok)
    begin
        if (instance.TryGetProperty("QueueType",setting))
        begin
            if (setting.ValueKind != JsonValueKind.String) then
            begin
                errorMessage = "The instance's QueueType property is not a string!"
                ok = false
            end
            else if (String.IsNullOrWhitespace(setting.GetString())) then
            begin
                errorMessage = "The instance's QueueType property is blank!"
                ok = false
            end
            else
            begin
                using setting.GetString().ToUpper() select
                ("ISAMFILE"),
                begin
                    Settings.QueueType = MessageQueueType.IsamFile
                end
                ("KAFKA"),
                begin
.ifdef DBLNET
                    Settings.QueueType = MessageQueueType.Kafka
.else
                    errorMessage = "Queue type " + setting.GetString() + " is only valid when running the .NET version of replicator."
                    ok = false
.endc
                end
                (),
                begin
                    errorMessage = "The instance's QueueType property must be set to IsamFile or Kafka!"
                    ok = false
                end
                endusing
            end
        end
    end

    ;SleepTime

    if (ok)
    begin
        if (instance.TryGetProperty("SleepTime",setting))
        begin
            if (setting.ValueKind != JsonValueKind.Number) then
            begin
                errorMessage = "The instance's SleepTime property is not a number!"
                ok = false
            end
            else if (setting.GetInt32() < 1) then
            begin
                errorMessage = "The instance's SleepTime property may not be less than 1!"
                ok = false
            end
            else
            begin
                Settings.SleepTime = setting.GetInt32()
            end
        end
    end

    ;StopOnError

    if (ok)
    begin
        if (instance.TryGetProperty("StopOnError",setting))
        begin
            if (setting.ValueKind != JsonValueKind.True && setting.ValueKind != JsonValueKind.False) then
            begin
                errorMessage = "The instance's StopOnError property is not a boolean!"
                ok = false
            end
            else
            begin
                Settings.StopOnError = setting.GetBoolean()
            end
        end
    end

    ;SystemLogging

    if (ok)
    begin
        if (instance.TryGetProperty("SystemLogging",setting))
        begin
            if (setting.ValueKind != JsonValueKind.True && setting.ValueKind != JsonValueKind.False) then
            begin
                errorMessage = "The instance's SystemLogging property is not a boolean!"
                ok = false
            end
            else
            begin
                Settings.SystemLogging = setting.GetBoolean()
            end
        end
    end

    ;VerboseLogging

    if (ok)
    begin
        if (instance.TryGetProperty("VerboseLogging",setting))
        begin
            if (setting.ValueKind != JsonValueKind.True && setting.ValueKind != JsonValueKind.False) then
            begin
                errorMessage = "The instance's VerboseLogging property is not a boolean!"
                ok = false
            end
            else
            begin
                Settings.VerboseLogging = setting.GetBoolean()
            end
        end
    end

    ;--------------------------------------------------------------------------------------------------------
    ;Shared settings

    ;EmailSettings
    if (ok)
    begin
        if (doc.RootElement.TryGetProperty("EmailSettings",smtpsettings))
        begin
            if (smtpsettings.ValueKind != JsonValueKind.Object)
            begin
                errorMessage = "Configuration file EmailSettings property is not an object!"
                ok = false
            end
        end
    end

    ;SmtpServer
    if (ok)
    begin
        if (smtpsettings.TryGetProperty("SmtpServer",setting))
        begin
            if (setting.ValueKind != JsonValueKind.String) then
            begin
                errorMessage = "The instance's SmtpServer property is not a string!"
                ok = false
            end
            else
            begin
                Settings.SmtpServer = setting.GetString()
            end
        end
    end

    ;SmtpPort
    if (ok)
    begin
        if (smtpsettings.TryGetProperty("SmtpPort",setting))
        begin
            if (setting.ValueKind != JsonValueKind.Number) then
            begin
                errorMessage = "The instance's SmtpPort property is not a number!"
                ok = false
            end
            else if (setting.GetInt32() < 1) then
            begin
                errorMessage = "The instance's SmtpPort property is invalid!"
                ok = false
            end
            else
            begin
                Settings.SmtpPort = setting.GetInt32()
            end
        end
    end

    ;SmtpUseSSL
    if (ok)
    begin
        if (instance.TryGetProperty("SmtpUseSSL",setting))
        begin
            if (setting.ValueKind != JsonValueKind.True && setting.ValueKind != JsonValueKind.False) then
            begin
                errorMessage = "The instance's SmtpUseSSL property is not a boolean!"
                ok = false
            end
            else
            begin
                Settings.SmtpUseSSL = setting.GetBoolean()
            end
        end
    end

    ;SmtpUsername
    if (ok)
    begin
        if (smtpsettings.TryGetProperty("SmtpUsername",setting))
        begin
            if (setting.ValueKind != JsonValueKind.String) then
            begin
                errorMessage = "The instance's SmtpUsername property is not a string!"
                ok = false
            end
            else
            begin
                Settings.SmtpUsername = setting.GetString()
            end
        end
    end

    ;SmtpPassword
    if (ok)
    begin
        if (smtpsettings.TryGetProperty("SmtpPassword",setting))
        begin
            if (setting.ValueKind != JsonValueKind.String) then
            begin
                errorMessage = "The instance's SmtpPassword property is not a string!"
                ok = false
            end
            else
            begin
                Settings.SmtpPassword = setting.GetString()
            end
        end
    end

    ;EmailSenderAddress
    if (ok)
    begin
        if (smtpsettings.TryGetProperty("EmailSenderAddress",setting))
        begin
            if (setting.ValueKind != JsonValueKind.String) then
            begin
                errorMessage = "The instance's EmailSenderAddress property is not a string!"
                ok = false
            end
            else
            begin
                Settings.EmailSenderAddress = setting.GetString()
            end
        end
    end

    ;EmailSenderName
    if (ok)
    begin
        if (smtpsettings.TryGetProperty("EmailSenderName",setting))
        begin
            if (setting.ValueKind != JsonValueKind.String) then
            begin
                errorMessage = "The instance's EmailSenderName property is not a string!"
                ok = false
            end
            else
            begin
                Settings.EmailSenderName = setting.GetString()
            end
        end
    end

    ;EmailRecipients
    if (ok)
    begin
        if (!smtpsettings.TryGetProperty("EmailRecipients",setting)) then
        begin
            errorMessage = "EmailSettings does not have a EmailRecipients property!"
            ok = false
        end
        else if (setting.ValueKind != JsonValueKind.Array) then
        begin
            errorMessage = "EmailRecipients property is not an array!"
            ok = false
        end
        else if (setting.GetArrayLength() == 0) then
        begin
            errorMessage = "EmailRecipients property is an empty array!"
            ok = false
        end
        else
        begin
            data addressCount = setting.GetArrayLength()
            data addressNumber, int

            Settings.EmailRecipients = new String[addressCount]

            for addressNumber from 0 thru addressCount - 1
            begin
                emailAddress = setting[addressNumber]
                
                if (emailAddress.ValueKind != JsonValueKind.String) then
                begin
                    errorMessage = "EmailRecipients " + %string(addressNumber) + " is not a string!"
                    ok = false
                end
                else if (String.IsNullOrWhitespace(emailAddress.GetString())) then
                begin
                    errorMessage = "EmailRecipients " + %string(addressNumber) + " is blank!"
                    ok = false
                end
                else
                begin
                    Settings.EmailRecipients[addressNumber+1] = emailAddress.GetString()
                end
            end
        end
    end

    freturn ok

endfunction
