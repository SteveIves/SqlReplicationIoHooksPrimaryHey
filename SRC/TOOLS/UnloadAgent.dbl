
import ReplicationLibrary

;;; <summary>
;;; The main entry point for the application.
;;; </summary>
main UnloadAgent
.align
    record
        ok, boolean
        kafkaConnected, boolean
        chIn, i4
        chOut, i4
        chInOpen, i4
        chOutOpen, i4
        iobuffer, a32768
        errorText, a256
        errorMessage, string
        agentSettings, @UnloadAgentSettings
        validTables, [#]string
    endrecord

    external function
        xsubr, ^val
        KAFKA_INIT, i
        KAFKA_SHUTDOWN, i
        KAFKA_SEND, i
        KAFKA_POLL, i
        KAFKA_SUBSCRIBE, i
        KAFKA_RECEIVE, i
        KAFKA_LATEST_OFFSET, i
        KAFKA_COMMIT, i
    endexternal

proc
    xcall flags(87004000,1)
    xcall setctl("C",0,"Y",0)

    if (false)
    begin
        xcall UnloadAgentFakeCalls
    end

    ;Make sure we're on OpenVMS

    begin
        data system, i4
        xcall envrn(system)
        if (system != 6)
        begin
            Console.WriteLine("This program is for use on OpenVMS. On other platforms use NetUnloadAgent")
            sleep 3
            stop
        end
    end

    ;Load configuration

    agentSettings = UnloadAgentSettings.Load(errorMessage)
    if (agentSettings == ^null)
    begin
        Console.WriteLine("ERROR: Failed to load configuration. Error was: " + errorMessage)
        sleep 3
        stop
    end

    ;Set environment variables defined in the configuration file

    if (agentSettings.EnvironmentVariables != ^null)
    begin
        data envvar, @EnvironmentVariable
        data sts, i4
        foreach envvar in agentSettings.EnvironmentVariables
        begin
            data p1, string, envvar.Name
            data p2, string, envvar.Value
            xcall setlog(envvar.Name,envvar.Value,sts)
        end
    end

    ;Report configuration

    Console.WriteLine("")
    Console.WriteLine("")
    Console.WriteLine("File Unload Agent")
    Console.WriteLine("")
    Console.WriteLine("Kafka brokers      : " + agentSettings.KafkaBrokers)
    Console.WriteLine("Consumer group     : " + agentSettings.ConsumerGroup)
    Console.WriteLine("Request topic      : " + agentSettings.RequestTopic)
    Console.WriteLine("Response topic     : " + agentSettings.ResponseTopic)
    Console.WriteLine("Consumer timeout   : " + %string(agentSettings.RequestTimeoutMs) + " ms")
    Console.WriteLine("Export directory   : " + agentSettings.ExportDirectory)
    Console.WriteLine("Zip file directory : " + agentSettings.ZipFileDirectory)
    Console.WriteLine("")

    ;Connect to Kafka 

    log("Connecting to Kafka")

    if (%KAFKA_INIT(agentSettings.KafkaBrokers,agentSettings.ConsumerGroup,errorText) == 0) then
        kafkaConnected = true
    else
        log("ERROR: Connect failed: " + %atrim(errorText))

    ok = true

    try
    begin
        ;Subscribe to the topic for inbound messages

        if (ok)
        begin
            log("Subscribing to " + agentSettings.RequestTopic)

            if (%KAFKA_SUBSCRIBE(agentSettings.RequestTopic,errorText) == -1)
            begin
                log("ERROR: Subscribe failed: " + %atrim(errorText))
                ok = false
            end
        end

        ;Listen for messages, which have an optional key (we don't use it) and a value. We will respond to:
        ; TABLES <table1>,<table2>,...,<tablen>         ;List of valid table names.
        ; UNLOAD <table>
        ; EXIT

        if (ok)
        begin
            repeat
            begin
                data topic, a40
                data message, a4096, ""
                data messageSize, i4, ^size(message)
                data partition, i4
                data offset, i8
                data receiveStatus, i4

                ok = true

                receiveStatus = %KAFKA_RECEIVE(agentSettings.RequestTimeoutMs,topic,message,messageSize,partition,offset,errorText)

                upcase message

                ;------------------------------------------------------------------------------------------------------------------------
                if (receiveStatus == -2) then
                begin
                    ;Timeout. Chance to do routine processing, otherwise just try again.
                    nextloop
                end
                ;------------------------------------------------------------------------------------------------------------------------
                else if (receiveStatus == -1) then
                begin
                    ;Hard error (network error, etc.)
                    log("ERROR: Receive failed: " + %atrim(errorText))
                    ;TODO: Now what? Disconnect, reconnect, try again?


                end
                ;------------------------------------------------------------------------------------------------------------------------
                else if (%atrim(topic).ne.agentSettings.RequestTopic) then
                begin
                    ;Should never happen because we only subscribed to a single topic, but just incase,
                    ;we'll commit the message so that we never receive it again
                    log("ERROR: Message from unsubscribed topic: " + %atrim(topic))

                    if (%KAFKA_COMMIT(agentSettings.RequestTopic,partition,offset+1,errorText) == 0) then
                    begin
                        nextloop
                    end
                    else
                    begin
                        log("ERROR: Commit failed: " + %atrim(errorText))
                        ;TODO: Now what? Disconnect, reconnect, try again?


                    end
                end
                ;------------------------------------------------------------------------------------------------------------------------
                else if (messageSize>=4 && message.eq."STOP") then
                begin
                    log("Received STOP instruction")
                    ;;Commit the message
                    if (%KAFKA_COMMIT(agentSettings.RequestTopic,partition,offset+1,errorText) == 0) then
                    begin
                        exitloop
                    end
                    else
                    begin
                        log("ERROR: Commit failed: " + %atrim(errorText))
                        ;TODO: Now what? Disconnect, reconnect, try again?


                    end
                end
                ;------------------------------------------------------------------------------------------------------------------------
                else if (messageSize>7 && message.eq."UNLOAD") then
                begin
                    ;UNLOAD <table>
                    data fileSpec, string
                    data exportFile, string
                    data exportFileSpec, string
                    data zipName, string 
                    data zipSpec, string
                    data fileType, string
                    data fileExported = false
                    data fileZipped = false
                    data fileUploaded = false
                    data recordLength, int

                    ;Extact the table name from the message
                
                    data tableName, string, message(8,messageSize)

                    ;Do we have vailid table names, and if so is the table name valid

                    if (validTables!=^null && validTables.Length>0) then
                    begin
                        if (Array.IndexOf(validTables,tableName,1,validTables.Length) >= 1) then
                        begin
                            log("Received UNLOAD instruction for " + tableName)
                        end
                        else
                        begin
                            ok = false
                            log("ERROR: Received UNLOAD instruction for invalid table " + tableName)
                        end
                    end
                    else
                    begin
                        ;Bad request
                        ok = false
                        log("ERROR: Can't process UNLOAD requests until valid table names are received")
                    end

                    ;Determine the file type and record length
                    if (ok)
                    begin
                        data tmpFileSpec, a80
                        xcall xsubr(tableName+"File",tmpFileSpec)
                        fileSpec = %atrimtostring(tmpFileSpec)

                        recordLength = %xsubr(tableName+"Length")
                    end

                    ;Set the export and zip file names
                    if (ok)
                    begin
                        exportFile = tableName + ".TXT"
                        exportFileSpec =agentSettings.ExportDirectory + exportFile
                        zipName = tableName + ".ZIP"
                        zipSpec = agentSettings.ZipFileDirectory + zipName
                    end

                    ;Determine the file type
                    if (ok)
                    begin
                        data tmptype, a8
                        xcall xsubr(tableName+"Type",tmptype)
                        fileType = tmptype
                    end

                    ;Open the input file

                    if (ok)
                    begin
                        chIn = %xsubr(tableName+"OpenInput",errorText)
                        if (chIn) then
                        begin
                            chInOpen = true
                        end
                        else
                        begin
                            chInOpen = false
                            ok = false
                            log("ERROR: Failed to open input file: " + %atrim(errorText))
                        end
                    end

                    ;Open the output file

                    if (ok)
                    begin
                        try
                        begin
                            open(chOut=0,o:s,exportFileSpec)
                            chOutOpen = true
                        end
                        catch (e, @Exception)
                        begin
                            chOutOpen = false
                            ok = false
                            log("ERROR: Failed to open output file: " + e.Message)
                        end
                        endtry
                    end

                    ;Export the input file content

                    if (ok)
                    begin
                        data recs = 0

                        repeat
                        begin
;BIG DEAL!!!
;
;TODO: Need stop take into account dd_tag values?
;
;
                            reads(chIn,iobuffer(1:recordLength),eof)
                            recs += 1
                            writes(chOut,iobuffer(1:recordLength))
                        end
                    eof,
                        fileExported = true
                        log("Exported " + %string(recs) + " records to " + exportFileSpec)
                    end

                    ;Close the input and output files

                    if (chInOpen && chIn)
                    begin
                        close chIn
                        clear chIn
                    end

                    if (chOutOpen && chOut)
                    begin
                        close chOut
                        clear chOut
                    end

                    ;Compress the exported file

                    if (fileExported)
                    begin
                        ;TODO: zip the file
                        data command = "|@REPLICATOR_EXE:COMPRESS.COM " + exportFileSpec + " " + zipSpec
                        data pipeCh, i4
                        data msg, a128
                        try
                        begin
                            log("Executing command: " + command)
                            open(pipeCh=0,i,command)
                            ;COMPRESS.COM should always display just one message, either "SUCCESS" or "ERROR: <text>"
                            repeat
                            begin
                                reads(pipeCh,msg)
                                if (msg.eq."SUCCESS") then
                                begin
                                    log("Created " + exportFileSpec)
                                    fileZipped = true
                                    exitloop
                                end
                                else if (msg.eq."ERROR:") then
                                begin
                                    ok = false
                                    log(%atrim(msg))
                                    exitloop
                                end
                                else
                                begin
                                    log("ERROR: " + %atrim(msg))
                                end
                            end
                        end
                        catch (ex, @EndOfFileException)
                        begin
                            log("ERROR: Failed to compress export file")
                        end
                        catch (ex, @Exception)
                        begin
                            ok = false
                            log("ERROR: Failed to compress export file: " + ex.Message)
                        end
                        finally
                        begin
                            close pipeCh
                        end
                        endtry
                    end

                    ;Upload the compressed file to an S3 bucket

;                    if (fileZipped)
;                    begin
;                        ;TODO: Upload the ZIP file to S3
;                        nop
;                    end

                    ;Report the outcome to the outbound Kafka topic
 
;                    if (ok && fileUploaded) then
;                    begin
;                        ;Send "load file ready"
;                        if (!%KafkaPublish(publisher,"UPLOADED",(a)fileSpec))
;                        begin
;                            ;TODO: What to do on a publish fail?
;                            nop
;                        end
;                    end
;                    else
;                    begin
;                        ;Send "export failed"
;                        if (!%KafkaPublish(publisher,"UPLOADFAILED",(a)fileSpec))
;                        begin
;                            ;TODO: What to do on a publish fail?
;                            nop
;                        end
;                    end

                    ;Delete the compressed file

;                    if (fileZipped)
;                    begin
;                        try
;                        begin
;                            xcall delet(zipSpec)
;                        end
;                        catch (e, @Exception)
;                        begin
;                            nop
;                        end
;                        endtry
;                    end

                    ;;Commit the original message

                    if (%KAFKA_COMMIT(agentSettings.RequestTopic,partition,offset+1,errorText) == -1)
                    begin
                        log("ERROR: Commit failed: " + %atrim(errorText))
                        ;TODO: Now what? Disconnect, reconnect, try again?


                    end
                end
                ;------------------------------------------------------------------------------------------------------------------------
                else if (messageSize>7 && message.eq."TABLES ") then
                begin
                    data tableList, string, message(8:messageSize)
                    if (!tableList.Contains(",")) then
                    begin
                        ok = false
                        log("ERROR: Received incorrectly formatted TABLES message. Table names should be comma-delimited.")
                    end
                    else
                    begin
                        validTables = tableList.Split(",")
                        log("Received " + %string(validTables.Length) + " table names")
                    end
;
;TODO: Need to send success or failure confirmation to the UnloadResponse topic
;
                    if (%KAFKA_COMMIT(agentSettings.RequestTopic,partition,offset+1,errorText) == 0) then
                    begin
                        nextloop
                    end
                    else
                    begin
                        log("ERROR: Commit failed: " + %atrim(errorText))
                        ;TODO: Now what? Disconnect, reconnect, try again?


                    end
                end
                ;------------------------------------------------------------------------------------------------------------------------
                else
                begin
                    ;Unsupported message.

                    log("WARNING: Ignoring unexpected message: " + %atrim(message))

                    if (%KAFKA_COMMIT(agentSettings.RequestTopic,partition,offset+1,errorText) == 0) then
                    begin
                        nextloop
                    end
                    else
                    begin
                        log("ERROR: Commit failed: " + %atrim(errorText))
                        ;TODO: Now what? Disconnect, reconnect, try again?


                    end
                end
                ;------------------------------------------------------------------------------------------------------------------------
            end
        end
    end
    finally
    begin
        ;Disconnect from Kafka
        if (kafkaConnected)
        begin
            log("Disconnecting from Kafka")

            if (%KAFKA_SHUTDOWN(errorText) == 0) then
                kafkaConnected = false
            else
                log("ERROR: Failed to disconnect from Kafka: " + %atrim(errorText))
        end
    end
    endtry

    log("Stopping")
    stop

endmain

;;; <summary>
;;; Add a message to the console log
;;; </summary>
;;; <param name="message">Message to add</param>
subroutine log
    required in message, string
proc
    begin
        data now, a20, %datetime
        data timestamp, string, %string(^d(now(1:14)),"XXXX-XX-XX XX:XX:XX")
        Console.WriteLine(timestamp + " " + message)
    end
    xreturn
endsubroutine
